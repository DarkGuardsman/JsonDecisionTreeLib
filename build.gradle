import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'idea'
    id "io.freefair.lombok" version "6.0.0-m2"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = 'com.darkguardsman.jsondecisiontree'
version = '0.1.0'

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'src/example/java'
        }
        resources {
            srcDir 'src/main/resources'
            srcDir 'src/example/resources'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.guava:guava:11.0.2'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation files('./libs/JsonContentBuilderLib-0.4.0-all.jar')
    implementation 'com.google.code.findbugs:jsr305:3.0.1'

    //Junit5
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

jar {
    manifest {
        attributes(
                "Built-By": System.getProperty('user.name'),
                "Build-Timestamp": new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                "Build-Revision": getGitHash(),
                "Created-By": "Gradle ${gradle.gradleVersion}",
                "Build-Jdk": "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                "Build-OS": "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
                "Implementation-Title": project.name,
                "Implementation-Version": version,

        )
    }
}